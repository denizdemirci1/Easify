apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "kotlin-kapt"
apply plugin: "org.jlleitschuh.gradle.ktlint"
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'io.fabric'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.deniz.easify"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    buildTypes {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        def spotify_client_id = properties.getProperty('spotify_client_id')
        def spotify_client_secret = properties.getProperty('spotify_client_secret')

        debug {
            debuggable true
            minifyEnabled false
            buildConfigField "String", "SPOTIFY_CLIENT_ID", spotify_client_id
            buildConfigField "String", "SPOTIFY_CLIENT_SECRET", spotify_client_secret
        }
        release {
            minifyEnabled false
            buildConfigField "String", "SPOTIFY_CLIENT_ID", spotify_client_id
            buildConfigField "String", "SPOTIFY_CLIENT_SECRET", spotify_client_secret
        }
    }

    buildTypes.all {
        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    dataBinding {
        enabled = true
        enabledForTests = true
    }
}

ktlint {
    version = "0.35.0"
    debug = true
    verbose = true
    android = true
    outputToConsole = true
    disabledRules = ["max-line-length", "no-wildcard-imports"]
    filter {
        exclude("**/generated/**")
        include("**/java/**")
    }
}

kapt {
    generateStubs = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Spotify
    implementation project(':spotify-app-remote')
    implementation 'com.spotify.android:auth:1.1.0'

    // Lifecycle
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0-rc02'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.2.0-rc02"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0-rc02"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:2.1.0"
    implementation "androidx.navigation:navigation-ui-ktx:2.1.0"

    //Retrofit & OkHttp
    implementation 'com.squareup.retrofit2:retrofit:2.6.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.1'
    implementation 'com.squareup.okhttp3:okhttp:4.2.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.2.0'

    // Google
    implementation "com.google.code.gson:gson:2.8.6"
    implementation 'com.google.firebase:firebase-analytics:17.2.1'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    // Koin
    implementation "org.koin:koin-android:2.0.1"
    implementation 'org.koin:koin-androidx-viewmodel:2.0.1'
    implementation 'org.koin:koin-androidx-scope:2.0.1'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.10.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.10.0'

    // Kotlin & Android
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.2"
    kapt "com.android.databinding:compiler:3.2.0-alpha10"

    // Dependencies for local unit tests
    testImplementation "junit:junit:4.12"
    testImplementation "org.hamcrest:hamcrest-all:1.3"
    testImplementation "androidx.test:core-ktx:1.2.0"
    testImplementation "org.robolectric:robolectric:4.3.1"
    testImplementation "androidx.test.ext:junit-ktx:1.1.1"
    testImplementation "androidx.arch.core:core-testing:2.1.0"

    // AndroidX Test - Instrumented testing
    androidTestImplementation "androidx.test.ext:junit:1.1.1"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0-alpha03"

    //Material Dialog
    implementation 'com.afollestad.material-dialogs:core:3.1.0'

    // Other
    implementation 'com.makeramen:roundedimageview:2.3.0'
    implementation 'androidx.multidex:multidex:2.0.1'
}

apply plugin: 'com.google.gms.google-services'